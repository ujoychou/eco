################################################################################
# PRJ_DIR: project directory, which include: bin/doc/inc/src/test, .tmp/.bin
# INS_DIR:
# INC_DIRS
# LINK_LIBS
# LINK_LIB_DIRS
# SRC_DIRS: dedicated the source files to build.
# SRC_DIRS_EXCLUDE
# SRC_FILES_EXCLUDE
# 
# PRJ_VER_FILE: the output version file.
################################################################################

# project platform. win32/linux/win64
if (${PLATFORM} STREQUAL "linux-x64")
    add_definitions(-DECO_LINUX)
    message(STATUS "Platform is linux-x64")
elseif (${PLATFORM} STREQUAL "linux-aarch64")
    add_definitions(-DECO_LINUX)
    message(STATUS "Platform is linux-aarch64")
elseif (${PLATFORM} STREQUAL "win-x64")
    add_definitions(-DECO_WIN32)
    message(STATUS "Platform is win-x64")
elseif (${PLATFORM} STREQUAL "win-aarch64")
    add_definitions(-DECO_WIN32)
    message(STATUS "Platform is win-aarch64")
else()
    message(WARNING "Unknown platform: ${PLATFORM}")
endif()
# option: for ccmake.
option(MSVC_PCH "msvc precompile header file in windows." OFF)


################################################################################
# project version config name. [eco-linux-x64-debug-3.2.0]
set(PRJ_CFG ${PROJECT_NAME}_${PLATFORM}_${CMAKE_BUILD_TYPE}_${PROJECT_VERSION})
string(TOLOWER ${PRJ_CFG} PRJ_CFG)
# project version template file.
if(PRJ_VER_FILE)
    configure_file(${CMAKE_CURRENT_SOURCE_DIR}/Version.tpl.h ${PRJ_VER_FILE})
endif()
message("---------------------------------------------------------------------")
message("== ${PRJ_CFG}")
message("---------------------------------------------------------------------")

# project directory
set(TMP_DIR ${PRJ_DIR}/.tmp/${PRJ_CFG})
set(BIN_DIR ${PRJ_DIR}/.bin/${PRJ_CFG})
if (NOT INS_DIR)
    set(INS_DIR ${BIN_DIR})
endif()
message("== PRJ_DIR=${PRJ_DIR}")
message("== SRC_DIR=${SRC_DIRS}")
message("==.BIN_DIR=${BIN_DIR}")
message("==.TMP_DIR=${TMP_DIR}")
message("== INS_DIR=${INS_DIR}")
message("== CMAK_DIR=${CMAKE_CURRENT_SOURCE_DIR}")
message("== CBIN_DIR=${CMAKE_CURRENT_BINARY_DIR}")
message("---------------------------------------------------------------------")

# c++ target: output name. (exe/so/a)
if(${PRJ_FMT} STREQUAL "exe")
    add_executable(${PROJECT_NAME})
    set_target_properties(${PROJECT_NAME} PROPERTIES ARCHIVE_OUTPUT_DIRECTORY ${INS_DIR})
elseif(${PRJ_FMT} STREQUAL "shared")
    add_library(${PROJECT_NAME} SHARED)
    set_target_properties(${PROJECT_NAME} PROPERTIES LIBRARY_OUTPUT_DIRECTORY ${INS_DIR})
    set_target_properties(${PROJECT_NAME} PROPERTIES PREFIX "lib")
    set_target_properties(${PROJECT_NAME} PROPERTIES SUFFIX ".so.${PROJECT_VERSION}")
elseif(${PRJ_FMT} STREQUAL "static")
    add_library(${PROJECT_NAME} STATIC)
    set_target_properties(${PROJECT_NAME} PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${INS_DIR})
endif()


################################################################################
# include libs directory: 
target_include_directories(${PROJECT_NAME} PRIVATE ${INC_DIRS})
target_include_directories(${PROJECT_NAME} PRIVATE ${PRJ_DIR}/inc)
target_include_directories(${PROJECT_NAME} PRIVATE ${SRC_DIRS})
target_link_libraries(${PROJECT_NAME} PRIVATE ${LINK_LIBS})
target_link_directories(${PROJECT_NAME} PRIVATE ${LINK_LIB_DIRS})

# include libs directory: conan cmake generated by "conanfile.txt".
include(${TMP_DIR}/conanbuildinfo.cmake)
target_link_directories(${PROJECT_NAME} PRIVATE ${CONAN_LIB_DIRS})
target_include_directories(${PROJECT_NAME} PRIVATE ${CONAN_INCLUDE_DIRS})
message("== LIB=${LINK_LIBS}")
message("== INC_DIR=${INC_DIRS}")
message("== LIB_DIR=${LINK_LIB_DIRS}")
message("== INC_CONAN=${CONAN_INCLUDE_DIRS}")
message("== LIB_CONAN=${CONAN_LIB_DIRS}")
message("---------------------------------------------------------------------")


################################################################################
# pre compiler: protobuf
# pre compiler: msvc pch

# c++ compiler: c++
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11")
# c++ compiler: warning/error option.
# unuse -Werror for knowing who are warning. 
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra")
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wno-unused-parameter -Wno-switch")
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fvisibility=hidden")
# c++ compiler: shared dll.
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fPIC")
# c++ compiler: debug/release.
set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -ggdb -O0")
set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -Os")
# c++ compiler: "compile_commands.json".(used in vscode c_cpp_properties.json)
set(CMAKE_EXPORT_COMPILE_COMMANDS True)
# c++ compiler: logging
message("== CXX=${CMAKE_CXX_FLAGS}")
message("== CXX_DEBUG=${CMAKE_CXX_FLAGS_DEBUG}")
message("== CXX_RELEASE=${CMAKE_CXX_FLAGS_RELEASE}")
message("== CXX_COMPILE_COMMANDS=${CMAKE_EXPORT_COMPILE_COMMANDS}")
message("---------------------------------------------------------------------")


################################################################################
# c++ target: include cpp files.
if(SRC_DIRS)
    set(CXX_TEMP )
    file(GLOB_RECURSE CXX_TEMP ${SRC_DIRS}/*.cpp)
    list(APPEND SRC_FILES ${CXX_TEMP})
    file(GLOB_RECURSE CXX_TEMP ${SRC_DIRS}/*.cc)
    list(APPEND SRC_FILES ${CXX_TEMP})
    file(GLOB_RECURSE CXX_TEMP ${SRC_DIRS}/*.c)
    list(APPEND SRC_FILES ${CXX_TEMP})
endif()
# c++ target: exclude cpp files.
list(REMOVE_ITEM SRC_FILES ${SRC_FILES_EXCLUDE})
# c++ target: exclude cpp dirs.
if(SRC_DIRS_EXCLUDE)
    set(CXX_TEMP )
    file(GLOB_RECURSE CXX_TEMP ${SRC_DIRS_EXCLUDE}/*.cpp)
    list(REMOVE_ITEM SRC_FILES ${CXX_TEMP})
    file(GLOB_RECURSE CXX_TEMP ${SRC_DIRS_EXCLUDE}/*.cc)
    list(REMOVE_ITEM SRC_FILES ${CXX_TEMP})
    file(GLOB_RECURSE CXX_TEMP ${SRC_DIRS_EXCLUDE}/*.c)
    list(REMOVE_ITEM SRC_FILES ${CXX_TEMP})
endif()
# c++ target: bind to project.
target_sources(${PROJECT_NAME} PRIVATE ${SRC_FILES})

# c++ target: inc & lib & install.
message("== CPP=${SRC_FILES}")
message("---------------------------------------------------------------------")
